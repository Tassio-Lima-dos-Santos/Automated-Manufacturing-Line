FUNCTION_BLOCK FB_PRODUCT_QUEUE
VAR_INPUT
    push : BOOL := FALSE;
    pop  : BOOL := FALSE;
    clear  : BOOL := FALSE;
    itemIn : ST_PRODUCT;
END_VAR

VAR_OUTPUT
    itemOut : ST_PRODUCT;
    isEmpty : BOOL;
    isFull  : BOOL;
END_VAR

VAR
	trigPush : R_TRIG;
    trigPop  : R_TRIG;
    trigClear : R_TRIG;	

    buffer : ARRAY[1..100] OF ST_PRODUCT;
    head   : INT := 1;
    tail   : INT := 1;
    count  : INT := 0;
END_VAR

// Atualiza os detectores de flanco
trigPush(CLK := push);
trigPop(CLK := pop);
trigClear(CLK := clear);

// Peek
itemOut := buffer[head];

// Clear
IF trigClear.Q THEN
    head := 1;
    tail := head;
    count := 0;

// Dequeue
ELSIF trigPop.Q AND (count > 0) THEN
    head := (head MOD 100) + 1;
    count := count - 1;

// Enqueue
ELSIF trigPush.Q AND (count < 100) THEN
    buffer[tail] := itemIn;
    tail := (tail MOD 100) + 1;
    count := count + 1;
END_IF;

isEmpty := (count = 0);
isFull  := (count = 100);
